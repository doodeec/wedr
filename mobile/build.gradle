final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 0 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.doodeec.weather.android"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode VERSION_MAJOR * 10000000 + VERSION_MINOR * 100000 + VERSION_PATCH * 1000 + VERSION_BUILD
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
    }

    signingConfigs {
        release {
            // passwords and alias are obtained via askForPasswords task
//            storeFile file("../${project.property('keystore.file')}")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task askForPasswords << {
    def storePass
    def keyAlias
    def keyPass

    def keystorePropertiesFile = new File(project.property("keystore.properties"))

    if (project.hasProperty("keystore.properties") && keystorePropertiesFile.exists()) {
        println "Loading keystore passwords from property file..."
        Properties properties = new Properties()
        properties.load(new FileInputStream(keystorePropertiesFile))
        storePass = properties['keystore.store.password']
        keyAlias = properties['keystore.key.alias']
        keyPass = properties['keystore.key.password']
    } else {
        println "Getting keystore passwords from user input..."
        storePass = new String(System.console().readPassword("\nStore password: "))
        keyAlias = new String(System.console().readLine("Key alias: "))
        keyPass = new String(System.console().readPassword("Key password: "))
    }

    android.signingConfigs.release.storePassword = storePass
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPass
}


tasks.whenTaskAdded {
    theTask ->
        if (theTask.name.equals("packageRelease")) {
            theTask.dependsOn "askForPasswords"
        }
}

repositories {
    jcenter()
    maven {
        url 'http://lib.doodeec.com/artifactory/repo'

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.doodeec.utils:serverCommunicator:0.1.3'
}
